apply(plugin: "distribution")
apply(plugin: "node")
apply(plugin: "grunt")

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:0.4"
        classpath "com.moowork.gradle:gradle-grunt-plugin:0.5"
    }
}

distributions {
    main {
        contents {
            from { "dist" }
        }
    }
}

ext.srcDir = new File("src")
ext.bowerDir = new File("$srcDir/lib")

task processResources(dependsOn: ["npmInstall", "installGrunt", "bowerInstall"])

task build(type: GruntTask, dependsOn: processResources) {
    inputs.dir(srcDir)
    outputs.dir(buildDir)
    args = ["build"]

    doLast {
        tasks.distZip.execute()
    }
}

task test(type: GruntTask, dependsOn: build) {
    args = ["test"]
}

task run(type: GruntTask, dependsOn: build) {
    args = ["server:dist"]
}

task bowerInstall(type: NodeTask, dependsOn: "npmInstall") {
    inputs.file(file("bower.json"))
    outputs.dir(bowerDir)

    script = file("node_modules/bower/bin/bower")
    args = ["install"]
}

clean.dependsOn("grunt_clean")
grunt_clean.dependsOn(["npmInstall", "installGrunt"])
